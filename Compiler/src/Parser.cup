package compiler.parser;

import java_cup.runtime.Symbol;

class Parser;

parser code {:
 
    public void report_error(String message, Object info)
    {
        StringBuilder sb = new StringBuilder();
        sb.append("ERROR: ");
        sb.append(message).append(". ");
        
        if (info instanceof Symbol)
        {
            Symbol s = ((Symbol) info);
            sb.append("Token ").append(s.value);

            if (s.left >= 0)
            {                
                sb.append(" in line ").append(s.left);
                if (s.right >= 0)
                {
                    sb.append(", column ").append(s.right).append(".");
                }
            }
        }

        System.err.println(sb);
    }
   
    public void report_fatal_error(String message, Object info)
    {
        report_error(message, info);
        System.exit(1);    
    }

    public void syntax_error(Symbol current_token)
    {
        report_error("Syntax Error", current_token); 
    }

    public void unrecovered_syntax_error(Symbol current_token)
    {
        report_fatal_error("Couldn't repair and continue parse", current_token);
    }
    
:};

/* Terminals (tokens returned by the scanner). */

// Paréntesis.
terminal LPAREN, RPAREN, LBRACKET, RBRACKET;

// Operadores aritméticos y de asignación.
terminal PLUS, PLUS_PLUS, MINUS, MINUS_MINUS, DIV, TIMES, MOD, ASSIGNMENT, PLUS_EQUALS, MINUS_EQUALS, DIV_EQUALS, TIMES_EQUALS;

// Operadores de comparación.
terminal EQUALS, NOT_EQUALS, GREATER_THAN, LESS_THAN, GREATER_EQUALS_THAN, LESS_EQUALS_THAN;

// Operadores lógicos.
terminal AND, OR, XOR, NOT;

// Operadores de bit.
terminal SHIFT_LEFT, SHIFT_RIGHT, SHIFT_LEFT_EQUALS, SHIFT_RIGHT_EQUALS;

// Palabras reservadas.
terminal ARRAY, BEGIN, BOOLEAN, BYTE, CASE, CHAR, CONST, DO, DOWNTO, ELSE, END, FILE, FOR, FORWARD, FUNCTION, GOTO, IF, IN, INLINE, INT, LABEL, LONGINT, OF, PACKED, PROCEDURE, PROGRAM, READ, REAL, RECORD, REPEAT, SET, SHORTINT, STRING, THEN, TO, TYPE, UNTIL, VAR, WHILE, WITH, WRITE;

// Otros
terminal SEMI, COMMA, COLON, DOT;

// Terminales con valor.
terminal Object NIL;
terminal Integer NUMBER;
terminal Double DOUBLE;
terminal String XTRING;
terminal Byte XBYTE;
terminal Character CHARACTER;
terminal String IDENTIFIER;
terminal Boolean TRUE, FALSE;

/* Non terminals */
non terminal expr, expr_num, expr_identifier, expr_byte, term;
non terminal condition, condition_list;
non terminal identifier_list, data_type, array_data_type;
non terminal declaration, declaration_list, variable_declaration_block, declaration_block;
non terminal constant_declaration, constant_declaration_list, constant_declaration_block;
non terminal parameter, parameter_list, parameter_declaration_list, parameter_declaration, function_block, function;
non terminal procedure_block, procedure, code_block_item, code_block;
non terminal assignment_expr, function_call, sentence, sentence_item;
non terminal for_statement, while_statement, repeat_statement, if_statement, case_statement, case_list, case_item;
non terminal function_item, function_list, functions_declaration, program;

/* Precedences */
precedence left PLUS, MINUS;
precedence left TIMES, DIV, MOD;
precedence left AND, OR, XOR;
precedence left LPAREN, RPAREN;
precedence left PLUS_PLUS, MINUS_MINUS;

start with program;

expr ::= LPAREN expr RPAREN | function_call | expr_num | expr_identifier | expr_byte | term;

expr_num ::= expr_num PLUS expr_num
       | expr_num MINUS expr_num
       | expr_num TIMES expr_num
       | expr_num DIV expr_num
       | expr_num MOD expr_num
       | MINUS expr_num
       | NUMBER
       | DOUBLE; 

expr_identifier ::= expr_identifier PLUS_PLUS
                | expr_identifier MINUS_MINUS
                | PLUS_PLUS expr_identifier
                | MINUS_MINUS expr_identifier
                | IDENTIFIER;

expr_byte ::= expr_byte SHIFT_LEFT NUMBER
            | expr_byte SHIFT_RIGHT NUMBER
            | XBYTE;

term ::= XTRING
       | CHARACTER
       | TRUE
       | FALSE
       | NIL;

condition ::= LPAREN condition RPAREN
            | term EQUALS term      
            | term NOT_EQUALS term 
            | term GREATER_THAN term
            | term LESS_THAN term 
            | term GREATER_EQUALS_THAN term
            | term LESS_EQUALS_THAN term
            | TRUE 
            | FALSE;

condition_list ::= LPAREN condition_list RPAREN
                 | condition_list AND condition_list
                 | condition_list OR  condition_list
                 | condition_list XOR condition_list
                 | condition;                                    

identifier_list ::= identifier_list COMMA IDENTIFIER
                  | IDENTIFIER;

data_type ::= STRING | CHAR | REAL | BYTE | INT | LONGINT | SHORTINT | BOOLEAN;

array_data_type ::= CHAR | INT | LONGINT | SHORTINT;

declaration  ::= identifier_list COLON data_type SEMI
               | identifier_list COLON ARRAY LBRACKET NUMBER DOT DOT NUMBER RBRACKET OF array_data_type SEMI;

declaration_list ::= declaration_list declaration 
                   | declaration;

variable_declaration_block ::= VAR declaration_list END | /* Epsilon */ ;

constant_declaration ::= IDENTIFIER EQUALS expr SEMI;

constant_declaration_list ::= constant_declaration_list constant_declaration
                            | constant_declaration;

constant_declaration_block ::= CONST constant_declaration_list END | /* Epsilon */ ;

declaration_block ::= constant_declaration_block variable_declaration_block;

parameter_declaration ::= data_type IDENTIFIER;

parameter_declaration_list ::= parameter_declaration_list COMMA parameter_declaration
                 | parameter_declaration;

assignment_expr ::= IDENTIFIER ASSIGNMENT expr
                    | IDENTIFIER PLUS_EQUALS expr
                    | IDENTIFIER MINUS_EQUALS expr
                    | IDENTIFIER DIV_EQUALS expr
                    | IDENTIFIER TIMES_EQUALS expr
                    | IDENTIFIER SHIFT_LEFT_EQUALS NUMBER
                    | IDENTIFIER SHIFT_RIGHT_EQUALS NUMBER;
    
parameter_list ::= parameter_list COMMA expr
                | expr
                | /* Epsilon */;

function_call ::= IDENTIFIER LPAREN parameter_list RPAREN;

sentence_item ::= assignment_expr | expr;

sentence ::= sentence_item SEMI;

code_block_item ::= sentence | for_statement | while_statement | repeat_statement | if_statement | case_statement;

code_block ::= code_block code_block_item
             | code_block_item;

function_block ::= declaration_block code_block;

function ::= FUNCTION IDENTIFIER LPAREN parameter_declaration_list RPAREN COLON data_type BEGIN function_block END;

procedure_block ::= declaration_block code_block;

procedure ::= PROCEDURE IDENTIFIER LPAREN parameter_declaration_list RPAREN BEGIN procedure_block END;

for_statement ::= FOR IDENTIFIER ASSIGNMENT expr TO expr DO BEGIN code_block END;

while_statement ::= WHILE condition_list DO BEGIN code_block END;

repeat_statement ::= REPEAT code_block UNTIL condition_list;

if_statement ::= IF condition_list THEN sentence ELSE sentence
                | IF condition_list THEN sentence; 

case_item ::= expr COLON sentence;

case_list ::= case_list case_item
            | case_item;

case_statement ::= CASE IDENTIFIER OF case_list ELSE sentence;

function_item ::= function | procedure;

function_list ::= function_list function_item
                | function_item;

functions_declaration ::= function_list | /* Epsilon */;

program ::= PROGRAM IDENTIFIER declaration_block functions_declaration BEGIN code_block END;
